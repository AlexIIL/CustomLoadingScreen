// DON'T TOUCH THE BUILDSCRIPT[] BLOCK
// its special, and it is only there to make ForgeGradle work correctly.

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

apply plugin: "net.minecraftforge.gradle.forge" // adds the forge dependency
apply plugin: 'maven' // for uploading to a maven repo

version = "1.5.0"
// Feel free to rename this at some point
group= "alexiil.mc.mod.load"
archivesBaseName = "CustomLoadingScreen"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    maven {
        name = "BuildCraft"
        url = "https://mod-buildcraft.com/maven"
    }
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    testCompile "junit:junit:4.12"
    shade "com.mod-buildcraft:buildcraft-libexpression:0.9.1"
}

reobfJar {
    addExtraSrgLine("PK: buildcraft/lib/expression alexiil/mc/mod/load/repackage/buildcraft/lib/expression")
}

ext.configFile = file "build.properties"
configFile.withReader {
    // Load config. It shall from now be referenced as simply config or project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = "${project.config.mc_version}-${project.config.mod_version}"

minecraft {
    version = config.mc_version + "-" + config.forge_version
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not allways work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = config.mcp_mappings

    replaceIn "alexiil/mods/load/Lib.java"
    replace "@VERSION@", "${project.config.mod_version}"
    replace "@COMMIT_HASH@", System.getenv("DRONE_COMMIT")?: "manual build" 
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

sourceSets {
    main {
        java {
            srcDir "src/main/java"
        }
        resources {
            srcDir "src/main/resources"
        }
    }
    test {
        java {
            srcDir "src/test/java"
        }
    }
}

jar {
    configurations.shade.forEach { dep ->
        from(project.zipTree(dep)) {
            exclude "META-INF", "META-INF/**"
        }
    }
	manifest {
		attributes 'FMLCorePluginContainsFMLMod': 'true'
		attributes 'FMLCorePlugin': 'alexiil.mc.mod.load.coremod.ClsPlugin'
	}
}
